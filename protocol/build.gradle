plugins {
	id 'java'
	id 'com.github.johnrengelman.shadow' version '2.0.0'
}
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

project.ext {
	akka_version = '2.5.3'
	logback_version = '1.2.3'
	validator_version = '1.6'
	annotations_version = '12.0'
}

dependencies {
	compile group: 'com.typesafe.akka', name: 'akka-actor_2.11', version: rootProject.ext.akka_version
	compile group: 'com.typesafe.akka', name: 'akka-remote_2.11', version: rootProject.ext.akka_version
	compile group: 'com.typesafe.akka', name: 'akka-slf4j_2.11', version: rootProject.ext.akka_version
	compile group: 'ch.qos.logback', name: 'logback-classic', version: rootProject.ext.logback_version
	compile group: 'commons-validator', name: 'commons-validator', version: rootProject.ext.validator_version
	compile group: 'javax.json', name: 'javax.json-api', version: '1.1'
	compile group: 'org.glassfish', name: 'javax.json', version: '1.1'
	compileOnly group: 'com.intellij', name: 'annotations', version: rootProject.ext.annotations_version
}

/**
 * Compile the code and create a fat JAR archive with
 * all the dependencies needed to run the project.
 */
//noinspection UnnecessaryQualifiedReference
task node(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
	archiveName = 'ds2.jar'
	description = "DS2 project - A Gossip Style Failure Detection Service"
	manifest.attributes 'Main-Class': 'it.unitn.ds2.gsfd.Main'
	from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
	configurations = [project.configurations.runtime]
	//noinspection UnnecessaryQualifiedReference
	transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
		//noinspection GroovyAssignabilityCheck
		resource = 'reference.conf'
	}
}
